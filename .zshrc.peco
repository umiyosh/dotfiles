# peco
alias -g P='| peco'

function cdr() {
  dirs -l|perl -pe 's/ /\n/g'| peco | pecoAfterAction cd
}

function peco-pkill() {
    for pid in `ps aux | peco | awk '{ print $2 }'`
    do
        kill $pid
        echo "Killed ${pid}"
    done
}
alias pk="peco-pkill"

function agv () {
  # TODO:AfterActionの整理
  ag $@  | peco  | awk -F : '{print "-c " $2 " " $1}' | pecoAfterAction vi
}

function ackv () {
  # TODO:AfterActionの整理
  ack $@  | peco | awk -F : '{print "-c " $2 " " $1}' | pecoAfterAction vi
}

function lsp(){
  ls $@| peco | awk '{print $NF}' | pecoAfterAction "vi" "cd" "gzip" "rm -ri"
}
function findp() {
  # TODO:AfterActionの整理
  find $@ | peco | pecoAfterAction vi
}
alias fpe=findp

function locatep() {
  # TODO:AfterActionの整理
  locate $@ | peco | pecoAfterAction vi
}
alias lpe=locatep

function mdfindp() {
  # TODO:AfterActionの整理
  mdfind $@ | peco | pecoAfterAction vi
}
alias mpe=mdfindp

function pecoAfterAction() {
  # TODO:任意コマンドの受付
  Actions=($@)
  Source=($( < /dev/stdin  ))
  if [[ $#Actions -gt 1 ]]; then
  # TODO:promptをACTIONにする
    Action=$(for Action in $Actions ;do echo $Action;done|peco --prompt "SELECT ACTION >>"|head -1)
  else
    Action=$Actions
  fi
  if [[ -z $Source ]]; then
    echo
    echo nothing selected
    return
  fi
  eval $Action $Source < /dev/tty
}

# Widgets
## git add をpecoでする
## http://petitviolet.hatenablog.com/entry/20140722/1406034439
function peco-select-gitadd() {
    local SELECTED_FILE_TO_ADD="$(git status --porcelain | \
                                  peco --query "$LBUFFER" | \
                                  awk -F ' ' '{print $NF}')"
    if [ -n "$SELECTED_FILE_TO_ADD" ]; then
      BUFFER="git add $(echo "$SELECTED_FILE_TO_ADD" | tr '\n' ' ')"
      CURSOR=$#BUFFER
    fi
    zle accept-line
}
zle -N peco-select-gitadd
bindkey "^a^a" peco-select-gitadd
